/* (c) https://github.com/MontiCore/monticore */
plugins {
    id "java"
    id "monticore" //version "$mc_version" // MontiCore Plugin
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "6.0.0"
    // useful reports
    // todo: reactivate this plugin when new version compatible to gradle 6.0.1 is available
//    id 'cz.malohlava.visteg' version '1.0.5'
    id 'jacoco'
}


def guava_version = '30.0-jre'
def antlr_version = '4.7.1'
def junit_version = '4.13.1'
def grammar_classifier = "grammars"
def grammarDir = 'src/main/grammars'
def fd_version = "7.5.0"

group = "scolar"
version = '1.0.4-SNAPSHOT'
description = "Customization Interface DSL"
sourceCompatibility = "11"

buildDir = file("$projectDir/target")

// configure non-standard source sets
sourceSets {
    main.java.srcDirs += ["$projectDir/target/generated-sources/monticore/sourcecode"]
    grammars {
        resources {
            srcDirs([grammarDir])
            include "**/*.mc4"
        }
    }
}


dependencies {
    implementation "de.monticore:monticore-runtime:$mc_version"
    implementation "de.monticore:monticore-grammar:$mc_version"
    grammar("de.monticore:monticore-grammar:$mc_version") {
        capabilities {
            requireCapability("de.monticore:monticore-grammar-grammars")
        }
    }
    grammar "de.monticore.lang:fd-lang:$fd_version:$grammar_classifier"
    implementation "de.monticore.lang:fd-lang:$fd_version"

    grammar(project(':language-component-tool')) {
        //exclude group: 'de.monticore', module: 'monticore-grammar'
        capabilities {
            requireCapability("scolar:language-component-tool-grammars")
        }
    }
    implementation project(':language-component-tool')
    implementation group: 'de.monticore.lang', name: 'cd4analysis', version: mc_version
    grammar("de.monticore.lang:cd4analysis:$mc_version") {
        capabilities {
            requireCapability("de.monticore.lang:cd4analysis-grammars")
        }
    }
    implementation "de.se_rwth.commons:se-commons-logging:$se_commons_version"
    implementation "de.se_rwth.commons:se-commons-utilities:$se_commons_version"
    implementation group: 'com.google.guava', name: 'guava', version: guava_version
    implementation group: 'de.monticore.lang', name: 'cd4analysis', version: mc_version
    implementation group: 'org.antlr', name: 'antlr4-runtime', version: antlr_version
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    testImplementation group: 'junit', name: 'junit', version: junit_version
    testImplementation("de.monticore:monticore-runtime:$mc_version") {
        capabilities {
            requireCapability("de.monticore:monticore-runtime-tests")
        }
    }
}


task generateCustomizationInterface(type: MCTask) {
    grammar = file "$grammarDir/CustomizationConfiguration.mc4"
    outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
    def uptoDate = incCheck("CustomizationConfiguration.mc4")
    outputs.upToDateWhen { uptoDate }
}

compileJava {
    dependsOn project.collect { it.tasks.withType(MCTask) }
}

java {
    withSourcesJar()
    registerFeature('grammars') {
        usingSourceSet(sourceSets.grammars)
    }
}

sourcesJar.dependsOn project.collect { it.tasks.withType(MCTask) }

// generated java doc contains errors, disable for now
javadoc.failOnError(false)

// configure deployment
publishing {
    // configure what artifacts to publish
    publications {
        customization(MavenPublication) {
            artifactId = "$project.name"
            from components.java
        }
    }
    repositories.maven {
        credentials.username mavenUser
        credentials.password mavenPassword
        def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/scolar-releases/"
        def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/scolar-snapshots/"
        url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
    }
}

defaultTasks 'build'
